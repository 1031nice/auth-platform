plugins {
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.diffplug.spotless' version '6.23.3'
}

allprojects {
    group = 'com.auth'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'

    java {
        sourceCompatibility = '21'
        targetCompatibility = '21'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // Test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    spotless {
        java {
            googleJavaFormat()
            removeUnusedImports()
            indentWithSpaces(4)
            trimTrailingWhitespace()
            endWithNewline()
            target 'src/**/*.java'
            lineEndings 'UNIX'
        }
        
        format 'yaml', {
            target '**/*.yml', '**/*.yaml'
            indentWithSpaces(2)
            trimTrailingWhitespace()
            endWithNewline()
        }
    }
}

// Spring Boot BOM
dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.2.0'
    }
}
